/* rexx  __ANSIBLE_ENCODE_EBCDIC__ */
/*  REXX  */
/*  ISSUE A DATASET OF O.S. COMMANDS                    */
/*  SAME AS OSCMD1, BUT ONLY ACCEPTS SOLICITED MESSAGES */
/*   TRACE RESULTS */
ARG CMD
TOTDELAY = 7
PARSE  VAR CMD   NEWDELAY NEWCMD
LND = LENGTH(NEWDELAY)
IF  LND > 6  THEN  DO
   IF  SUBSTR(NEWDELAY,1,6) = "DELAY="  THEN  DO
      NDT = SUBSTR(NEWDELAY,7,LND-6)
      IF  DATATYPE(NDT,"W") = 1  THEN  DO
         TOTDELAY = NDT
         CMD = NEWCMD
         SAY "GETMSG MAXIMUM DELAY OF "TOTDELAY" SECONDS SELECTED "|,
             |"FOR COMMAND "CMD
      END
   END
END
ENDDELAY = TOTDELAY % 10 + 2
"CONSPROF  SOLD(NO) UNSOLD(NO)"
CPRC = RC
IF CPRC ^= 0 THEN DO
   SAY "CONSPROF COMMAND FAILED WITH RETURN CODE "CPRC
   SAY "IF CONSPROF NOT AUTHORISED," ,
       "ADD IT TO THE IKJTSOXX MEMBER"
   SAY "AND ACTIVATED WITH COMMAND  PARMLIB UPDATE(XX)"
   RETURN CPRC
END
CLID    = LEFT(MVSVAR("SYSCLONE"),2,"#")
TIMEVAR = RIGHT(TIME("S")//100,2,"0")
UID     = SYSVAR("SYSUID")
UIDLEN  = LENGTH(UID)
CARTNAME = CLID||TIMEVAR||SUBSTR(UID,UIDLEN-3,4)
/**  SAY "CART AND CONSOLE NAME IS "CARTNAME   **/
ACTCMD = "CONSOLE ACTIVATE NAME("CARTNAME") CART("CARTNAME") "
ACTCMD
CARC = RC
IF CARC ^= 0 THEN DO
   SAY "CONSOLE ACTIVATION COMMAND:"ACTCMD
   SAY "    FAILED WITH RETURN CODE "CARC
   RETURN CARC
END
SAY TIME()" ISSUING CMD "CMD
"CONSOLE SYSCMD("CMD") CART("CARTNAME")"
CSRC = RC
IF  CSRC ^= 0 THEN DO
   SAY "ISSUING OF COMMAND "CMD" FAILED WITH RETURN CODE "CSRC
   RETURN CSRC
END
RC4CT = 0
TOTREP = 0
DO  FOREVER
   TMG.0 = 0
   CMDRC = GETMSG("TMG.","SOL",CARTNAME,,1)
   IF  CMDRC > 4  THEN DO
      SAY "GETMSG FAILED WITH RETURN CODE "CMDRC
      RETURN CMDRC
   END
   IF  CMDRC = 0  &  TMG.0 > 0  THEN  DO
       DO  IJ = 1  BY 1  TO TMG.0
          QUEUE TMG.IJ
       END
       TOTREP = TOTREP + TMG.0
       RC4CT = 0
       ITERATE
   END
   IF  TOTREP > 0  THEN  DO
       IF RC4CT > ENDDELAY  THEN  LEAVE
   END;  ELSE  DO
      IF RC4CT > TOTDELAY  THEN  LEAVE
   END
   RC4CT = RC4CT + 1
END
SAY TIME()" "TOTREP" LINES RETRIEVED"
"CONSOLE DEACTIVATE"
CDRC = RC
IF CDRC ^= 0 THEN DO
   SAY "CONSOLE DEACTIVATION FAILED WITH RETURN CODE "CDRC
   RETURN CDRC
END
RETURN 0
